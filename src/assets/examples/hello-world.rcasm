;******************************************************
; Hello world demo for the relay computer
;******************************************************

        org	0x8400

hello:  ldi m,len       ; length of msg
        mov b,m2
        jsr negate      ; loop count in D
        clr a           ; zero the offset

hcont:  ldi m,mesg      ; get base of msg array
        mov m2,a        ; insert offset
        ldr B           ; get a char
        prt             ; print it
        mov b,a         ; get offset
        inc a           ; increment it
        mov b,d         ; get loop counter
        inc d           ; increment it
        bne hcont       ; finished?
        lds a           ; yes, check should stop
        mov b,a
        not d           ; double invert to test
        mov b,d
        not d
        beq hello       ; don't stop
        hlt             ; yes
        jmp hello       ; release halt to repeat

        org 0x8419
mesg:   dfb "Hello "
        dfb "world!"
        dfb 0x0D
        dfb 0x0D
        dfb "Relay "
        dfb "here. "
        dfb 0x0D
        dfb 0x0D
        dfb 0x0D
        dfb 0x0D

;******************************************************
; Negate subroutine for relay computer
;	Input:  0-255 in B
;	Output:	2's complement of B in D
;******************************************************

        org 0x843D
negate: not d       ; complement B to D
        mov b,d     ; move it back to B
        inc d       ; add one
        rts
